
Функция ПолучитьПозицию(Кластер, Тема, Раздел) Экспорт
	
	Отбор = Новый Структура("Кластер, Тема, Раздел", Кластер, Тема, Раздел);
	Возврат ПолучитьПоследнее(, Отбор).Позиция;	
	
КонецФункции

Процедура УстановитьПозицию(Кластер, Тема, Раздел, Позиция) Экспорт
	
	ТекДата = ТекущаяДата();
	Период = Дата(Год(ТекДата), Месяц(ТекДата), День(ТекДата), Час(ТекДата), 0, 0);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период .Установить(Период);
	НаборЗаписей.Отбор.Кластер.Установить(Кластер);
	НаборЗаписей.Отбор.Тема	  .Установить(Тема);
	НаборЗаписей.Отбор.Раздел .Установить(Раздел);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период		 = Период;
	Запись.Кластер		 = Кластер;
	Запись.Тема			 = Тема;
	Запись.Раздел		 = Раздел;
	Запись.Позиция		 = Позиция;
	Запись.ДатаУстановки = ТекДата;

	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УдалитьСтарые(Кластер, Тема, Раздел) Экспорт
	
	// Оставляем только последние 100 записей
	ОставлятьЗаписей = 100;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Кластер", Кластер);
	Запрос.УстановитьПараметр("Тема"   , Тема);
	Запрос.УстановитьПараметр("Раздел" , Раздел);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Колво
	|ИЗ
	|	РегистрСведений.КафкаПозиции КАК КафкаПозиции
	|ГДЕ
	|	КафкаПозиции.Кластер = &Кластер
	|	И КафкаПозиции.Тема = &Тема
	|	И КафкаПозиции.Раздел = &Раздел";
	КолвоВсего = Запрос.Выполнить().Выгрузить()[0].Колво;
	
	УдалитьЗаписей = КолвоВсего - ОставлятьЗаписей;	
	Если УдалитьЗаписей <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ " + Формат(УдалитьЗаписей, "ЧН=0; ЧГ=") + "
	|	КафкаПозиции.Период КАК Период
	|ИЗ
	|	РегистрСведений.КафкаПозиции КАК КафкаПозиции
	|ГДЕ
	|	КафкаПозиции.Кластер = &Кластер
	|	И КафкаПозиции.Тема = &Тема
	|	И КафкаПозиции.Раздел = &Раздел
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период .Установить(Выборка.Период);
		НаборЗаписей.Отбор.Кластер.Установить(Кластер);
		НаборЗаписей.Отбор.Тема	  .Установить(Тема);
		НаборЗаписей.Отбор.Раздел .Установить(Раздел);
		НаборЗаписей.Записать();
	КонецЦикла;
			
КонецПроцедуры
