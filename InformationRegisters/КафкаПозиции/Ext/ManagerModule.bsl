
Функция ПолучитьПозицию(Тема, Раздел) Экспорт
	
	Отбор = Новый Структура("Тема, Раздел", Тема, Раздел);
	Возврат ПолучитьПоследнее(, Отбор).Позиция;	
	
КонецФункции

Процедура УстановитьПозицию(Тема, Раздел, Позиция) Экспорт
	
	ТекДата = ТекущаяДата();
			
	МЗ = СоздатьМенеджерЗаписи();
	МЗ.Период = Дата(Год(ТекДата), Месяц(ТекДата), День(ТекДата), Час(ТекДата), 0, 0);
	МЗ.Тема = Тема;
	МЗ.Раздел = Раздел;
	МЗ.Позиция = Позиция;
	МЗ.ДатаУстановки = ТекДата;
	МЗ.Записать();
		
КонецПроцедуры

Процедура УдалитьСтарые(Тема, Раздел) Экспорт
	
	// Оставляем только последние 100 записей
	ОставлятьЗаписей = 100;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Тема"  , Тема);
	Запрос.УстановитьПараметр("Раздел", Раздел);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Колво
	|ИЗ
	|	РегистрСведений.КафкаПозиции КАК КафкаПозиции
	|ГДЕ
	|	КафкаПозиции.Тема = &Тема
	|	И КафкаПозиции.Раздел = &Раздел";
	КолвоВсего = Запрос.Выполнить().Выгрузить()[0].Колво;
	
	УдалитьЗаписей = КолвоВсего - ОставлятьЗаписей;	
	Если УдалитьЗаписей <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ " + Формат(УдалитьЗаписей, "ЧН=0; ЧГ=") + "
	|	КафкаПозиции.Период КАК Период
	|ИЗ
	|	РегистрСведений.КафкаПозиции КАК КафкаПозиции
	|ГДЕ
	|	КафкаПозиции.Тема = &Тема
	|	И КафкаПозиции.Раздел = &Раздел
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МЗ = СоздатьМенеджерЗаписи();
		МЗ.Период = Выборка.Период;
		МЗ.Тема	  = Тема;
		МЗ.Раздел = Раздел;
		МЗ.Удалить();
	КонецЦикла;
			
КонецПроцедуры
