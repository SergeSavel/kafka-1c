
Функция КафкаКонфигурация(ШинаСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ШиныОбмена.Ссылка КАК Ссылка,
	|	ШиныОбмена.Кластер КАК Кластер
	|ИЗ
	|	Справочник.КафкаШиныОбмена КАК ШиныОбмена
	|ГДЕ
	|	ШиныОбмена.Ссылка = &Шина
	|	И НЕ ШиныОбмена.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Конфигурация.Параметр КАК Параметр,
	|	Конфигурация.Значение КАК Значение
	|ИЗ
	|	Справочник.КафкаШиныОбмена.КафкаКонфигурация КАК Конфигурация
	|ГДЕ
	|	Конфигурация.Ссылка = &Шина");
	Запрос.УстановитьПараметр("Шина", ШинаСсылка);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Тогда
		ВызватьИсключение "Передан некорректный параметр.";
	КонецЕсли;
	
	ТЗ = РезультатЗапроса[0].Выгрузить();
	С = ТЗ[0];
	
	Результат = Справочники.КафкаКластеры.КафкаКонфигурация(С.Кластер);
		
	ТЗ = РезультатЗапроса[1].Выгрузить();
	Для Каждого С Из ТЗ Цикл
		Результат.Вставить(С.Параметр, С.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ОбменРасписаниеПоУмолчанию() Экспорт
	
	ОбменРасписание = Новый РасписаниеРегламентногоЗадания();
	ОбменРасписание.ПериодПовтораДней = 1;
	ОбменРасписание.ПериодПовтораВТечениеДня = 300;
	
	Возврат ОбменРасписание;
	
КонецФункции

Процедура ВыполнитьОбмен(ШинаСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ШиныОбмена.Ссылка КАК Ссылка,
	|	ШиныОбмена.ОбменПоРасписанию КАК ОбменПоРасписанию,
	|	ШиныОбмена.Вид КАК Вид
	|ИЗ
	|	Справочник.КафкаШиныОбмена КАК ШиныОбмена
	|ГДЕ
	|	ШиныОбмена.Ссылка = &Ссылка
	|	И НЕ ШиныОбмена.ЭтоГруппа");
	Запрос.УстановитьПараметр("Ссылка", ШинаСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ВызватьИсключение "Передан некорректный параметр.";
	КонецЕсли;
	
	Шина = РезультатЗапроса.Выгрузить()[0];
	
	Если Шина.ОбменПоРасписанию Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.УстановитьДанные(Шина.Ссылка);
		Сообщение.Поле = "ОбменПоРасписанию";
		Сообщение.Текст = "Перед ручным запуском обмен по расписанию должен быть выключен.";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Шина.Вид = "Источник" Тогда
		КафкаСервер.Загрузить(Шина.Ссылка);
	ИначеЕсли Шина.Вид = "Приемник" Тогда
		КафкаСервер.Выгрузить(Шина.Ссылка);
	Иначе
		ВызватьИсключение "Некорректный вид шины: """+Шина.Вид+""".";
	КонецЕсли;
	
КонецПроцедуры