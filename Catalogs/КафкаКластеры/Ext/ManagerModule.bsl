
Функция КафкаКонфигурация(Кластер) Экспорт
		
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Кластеры.КафкаСерверы КАК Серверы,
	|	Кластеры.КафкаПротоколБезопасности КАК ПротоколБезопасности,
	|	Кластеры.КафкаПользователь КАК Пользователь,
	|	Кластеры.КафкаПользовательУстановлен КАК ПользовательУстановлен,
	|	Кластеры.КафкаПароль КАК Пароль
	|ИЗ
	|	Справочник.КафкаКластеры КАК Кластеры
	|ГДЕ
	|	Кластеры.Ссылка = &Кластер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Конфигурация.Параметр КАК Параметр,
	|	Конфигурация.Значение КАК Значение
	|ИЗ
	|	Справочник.КафкаКластеры.КафкаКонфигурация КАК Конфигурация
	|ГДЕ
	|	Конфигурация.Ссылка = &Кластер");
	Запрос.УстановитьПараметр("Кластер", Кластер);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Тогда
		ВызватьИсключение "Передан некорректный параметр.";
	КонецЕсли;
	
	Результат = Новый Соответствие;
	
	ТЗ = РезультатЗапроса[0].Выгрузить();
	С = ТЗ[0];
	Результат.Вставить("bootstrap.servers", С.Серверы);
	Результат.Вставить("security.protocol", С.ПротоколБезопасности);
	Если СтрНачинаетсяС(ВРег(С.ПротоколБезопасности), "SASL") Тогда
		Результат.Вставить("sasl.mechanism", "PLAIN");
		Если С.ПользовательУстановлен Тогда
			Результат.Вставить("sasl.username", С.Пользователь);
		Иначе
			Результат.Вставить("sasl.username", КафкаПереопределяемый.ПользовательПоУмолчанию());
		КонецЕсли;
		Результат.Вставить("sasl.password", С.Пароль);
	КонецЕсли;
	
	ТЗ = РезультатЗапроса[1].Выгрузить();
	Для Каждого С Из ТЗ Цикл
		Результат.Вставить(С.Параметр, С.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции